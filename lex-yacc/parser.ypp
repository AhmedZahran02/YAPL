%{ 
    #include <iostream>
    #include <string>

    #include "branching.hpp"
    #include "common.hpp"
    #include "expression.hpp"
    #include "loop.hpp"
    #include "program.hpp"
    #include "scoping.hpp"
    #include "var-op.hpp"

    #include "parser.tab.hpp"
    extern int yylex();
    extern FILE *yyin;   
    void yyerror(const char* s) {
        std::cerr << "Error: " << s << std::endl;
    }
%}

%union  {
    ProgramNode*    Node;
    Expression*     Exp;
    char* str;
}


%token <str> IDENTIFIER STRING POW SQRT SHL SHR DATA_TYPE IF ELSE WHILE FOR AND OR GTE LTE LT GT ISEQUAL NOTEQUAL FUNCTIONDEF DO
%token <str> CHARACTER
%token <str> INTEGER BOOLEAN
%token <str> FLOAT
%token <str> PRINT

%type <Node>   ForSentence WhileSentence DoWhileSentence
%type <Node>   IFSentence VariableAssignment VariableDeclaration 
%type <Node>   PrintSentence Scope Sentence Sentences

%type <Exp>  MathOperand MathOperation Operation Condition
%type <Exp>  StringOperation StringOperand 


%left ';'
%right ISEQUAL NOTEQUAL PRINT
%left '|'
%left '^'
%left '&'
%left SHL SHR
%left LT GT LTE GTE
%left '+' '-'
%left '*' '/' '%'
%right '['
%left ']'
%left ')'
%right '('
%right IF
%right ELSE

%start Program

%%

Program: Sentences  {
    ScopeNode* scopeNode = new ScopeNode($1);
    scopeNode->run();
}


Sentences: 
      Sentence Sentences        { $$ = $1->setNext($2); }
   
    | Scope Sentences           { $$ = $1->setNext($2); } 

    |                           { $$ = nullptr; }

Scope:  '{' Sentences '}'       { $$ = new ScopeNode($2); }    

Sentence: 
      VariableAssignment        { $$ = $1; }
    | VariableDeclaration       { $$ = $1; }   
    | FunctionDefinition        { $$ = new ScopeNode(); }
    | IFSentence                { $$ = $1; }   
    | ForSentence               { $$ = $1; }
    | WhileSentence             { $$ = $1; }
    | DoWhileSentence           { $$ = $1; }
    | PrintSentence             { $$ = $1; }

PrintSentence:
    PRINT '(' Operation ')' ';' {
        $$ = new PrintNode($3);
    }

IFSentence:
      IF '(' Condition ')' Scope   { $$ = new IfNode($3, $5);   }
    | IFSentence ELSE IFSentence   { $$ = dynamic_cast<IfNode*>($1)->setElse($3);  }
    | IFSentence ELSE Scope        { $$ = dynamic_cast<IfNode*>($1)->setElse($3);  }

ForSentence: FOR '(' VariableDeclaration Condition ';' VariableAssignment ')' Scope {
    $$ = new ForNode(dynamic_cast<DefineNode*>($3), $4, dynamic_cast<AssignNode*>($6), dynamic_cast<ScopeNode*>($8));
}

WhileSentence: WHILE '(' Condition ')' Scope {
    $$ = new WhileNode($3, dynamic_cast<ScopeNode*>($5));
}

DoWhileSentence: DO Scope WHILE '(' Condition ')' ';' {
    $$ = new DoWhileNode($5, dynamic_cast<ScopeNode*>($2));
}

FunctionDefinition: FUNCTIONDEF IDENTIFIER '(' FunctionParams ')' Scope

FunctionParams: DATA_TYPE IDENTIFIER ',' FunctionParams | ;

VariableDeclaration: 
      DATA_TYPE IDENTIFIER ';'                      { $$ = new DefineNode($1, $2, nullptr); }
    | DATA_TYPE IDENTIFIER '=' Operation ';'        { $$ = new DefineNode($1, $2, $4); }

VariableAssignment: IDENTIFIER '=' Operation ';'    { $$ = new AssignNode($1, $3); }        

Condition:
        MathOperation OR MathOperation              { $$ = new Expression($1, $3, OP_OR);  }
    |   MathOperation AND MathOperation             { $$ = new Expression($1, $3, OP_AND); } 
    |   MathOperation GTE MathOperation             { $$ = new Expression($1, $3, OP_GE);  } 
    |   MathOperation GT MathOperation              { $$ = new Expression($1, $3, OP_GT);  } 
    |   MathOperation LTE MathOperation             { $$ = new Expression($1, $3, OP_LE);  } 
    |   MathOperation LT MathOperation              { $$ = new Expression($1, $3, OP_LT);  } 
    |   MathOperation ISEQUAL MathOperation         { $$ = new Expression($1, $3, OP_EQ);  } 
    |   MathOperation NOTEQUAL MathOperation        { $$ = new Expression($1, $3, OP_NEQ); } 

    |   StringOperation GTE StringOperation         { $$ = new StringContainer($1, $3, OP_GE);   }
    |   StringOperation GT StringOperation          { $$ = new StringContainer($1, $3, OP_GT);   }
    |   StringOperation LTE StringOperation         { $$ = new StringContainer($1, $3, OP_LE);   }
    |   StringOperation LT StringOperation          { $$ = new StringContainer($1, $3, OP_LT);   }
    |   StringOperation ISEQUAL StringOperation     { $$ = new StringContainer($1, $3, OP_EQ);   }
    |   StringOperation NOTEQUAL StringOperation    { $$ = new StringContainer($1, $3, OP_NEQ);  }

    |   MathOperation                               { $$ = $1; }
    |   StringOperation                             { $$ = $1; }

Operation: 
      MathOperation                                 { $$ = $1; /*std::cout << $1->getValue() << std::endl;*/ }     
    | StringOperation                               { $$ = $1; /*std::cout << $1->getValue() << std::endl;*/ }   

MathOperation:
        MathOperation '+' MathOperation             { $$ = new Expression($1, $3, OP_ADD); }
    |   MathOperation '-' MathOperation             { $$ = new Expression($1, $3, OP_SUB); }  
    |   MathOperation '*' MathOperation             { $$ = new Expression($1, $3, OP_MUL); } 
    |   MathOperation '/' MathOperation             { $$ = new Expression($1, $3, OP_DIV); }    
    |   MathOperation '%' MathOperation             { $$ = new Expression($1, $3, OP_MOD); }     
    |   '(' MathOperation ')' MathOperation         { $$ = new Expression($2, $4, OP_MUL); } 
    |   '(' MathOperation ')'                       {                   $$ = $2;               }
    |   POW '(' MathOperation ',' MathOperation ')' { $$ = new Expression($3, $5, OP_POW);  }
    |   SQRT '(' MathOperation ')'                  { $$ = new Expression($3, $3, OP_SQRT); }
    |   MathOperation SHL MathOperation             { $$ = new Expression($1, $3, OP_SHL); } 
    |   MathOperation SHR MathOperation             { $$ = new Expression($1, $3, OP_SHR); } 
    |   MathOperation '|' MathOperation             { $$ = new Expression($1, $3, OP_OR); }
    |   MathOperation '&' MathOperation             { $$ = new Expression($1, $3, OP_AND); }
    |   MathOperation '^' MathOperation             { $$ = new Expression($1, $3, OP_XOR); }
    |   MathOperand                    

StringOperation:
        StringOperation '+' StringOperation             { $$ =  new StringContainer($1, $3, OP_ADD); } 
    |   StringOperation '+' MathOperation               { $$ =  new StringContainer($1, $3, OP_ADD); } 
    |   StringOperation '-' MathOperation               { $$ =  new StringContainer($1, $3, OP_SUB); }    
    |   '(' StringOperation ')'                         { $$ =  $2; } 
    /* |   StringOperand '[' INTEGER ':' INTEGER ']'    { $$ =  new StringContainer($1, $3, OP_SHR); }  */
    |   StringOperation SHL StringOperation                 { $$ =  new StringContainer($1, $3, OP_SHL); } 
    |   StringOperation SHR StringOperation                 { $$ =  new StringContainer($1, $3, OP_SHR); } 
    |   StringOperand   

MathOperand:
          INTEGER       { $$ = new Expression($1, TINT);}

        | FLOAT         { $$ = new Expression($1, TFLOAT);}

        | CHARACTER     { $$ = new Expression($1, TCHAR);}

        | BOOLEAN       { $$ = new Expression($1, TBOOLEAN);}

        | IDENTIFIER    { $$ = new IdentifierContainer($1); }
    
StringOperand: STRING   { $$ = new StringContainer($1); }

%%


int main() {
    ScopeNode* x = new ScopeNode();
    yyin = fopen("test.yapl", "r");
    yyparse();
    return 0;
}